#+TITLE: Making Tetris for Haskell Beginners
#+JEKYLL_LAYOUT: post
#+DATE: 2023-07-18
#+OPTIONS: toc:nil

#+BEGIN_SRC haskell :exports none
:r
:m
:set +m
#+END_SRC

#+RESULTS:
: Ok, no modules loaded.

#+BEGIN_SRC haskell :exports none :results output
:set prompt-cont ""
#+END_SRC

#+RESULTS:

[[/img/tetriskell.gif]]\\
/We'll make this over the course of the tutorial/

* What This Is
This post is a hands-on introduction to Haskell via the implementation of a little-known game involving falling blocks, because that's how I first learnt the basics. I'll try explain almost everything, such that a competent programmer with no Haskell or even functional programming familiarity can follow it and end up with a passing understanding of building a simple Haskell application.

I'll explicitly try to overexplain everything, either in prose or in comments.

We'll end up with a minimal terminal implementation of Tetris, and a simple agent playing using [[https://en.wikipedia.org/wiki/Beam_search][beam search]].

* What This Isn't
I won't touch on package management or project structure - in fact, this post is a literate[fn:literate] Haskell file, and the concatenated code blocks can be run via ~runhaskell tetris.hs~. There are plenty of tutorials on package managers like Stack and Cabal, and on general project management out there - for now, all you need is whatever Haskell distribution your machine uses. [[https://www.haskell.org/ghcup/][GHCup]] is as good a place to start as any. You might already even have ~runhaskell~ on your machine.

We'll try to use as few external dependencies as possible, and won't use any language extensions.

There are a lot of ways one could write this code more cleanly and performantly - avoiding passing around explicit state using monad transformers like ~StateT~, being more careful around the use of strictness versus laziness, and so on - I'm considering this out of scope and will try keep it as simple as I can.

[fn:literate] Okay, not quite. I'm writing this in Emacs, where ~org-babel~ will run each block in GHCi, a Haskell interpreter, with ~set +m~ enabled to allow multiline blocks. The whole thing gets compiled to Markdown via ~ox-jekyll~. The end result is the same, more or less, as writing actual literate code.


* Prelude
I watched the [[https://en.wikipedia.org/wiki/Tetris_(film)][Tetris]] movie this week. There's this almost certainly apocryphal scene where Alexey Patjinov is demoing his creation to a publisher, who has a [[https://www.youtube.com/watch?v=PEgk2v6KntY]["drop the 'the'"]] moment and suggests all completed rows should vanish at once, rather than one at a time, enabling the achievement of the four-lines-at-once namesake move. They swiftly hack the feature together on a tiny monochrome display, and I was reminded how lucky I am to live in an era of rich tooling, expressive languages, and 4K monitors.

When I was first learning Haskell, though, it felt like punching holes in cards. I couldn't get my head around the interplay between the purity of the language and the need to interact with the real world. A long while before, I'd grokked Gary Bernhardt's [[https://www.destroyallsoftware.com/screencasts/catalog/functional-core-imperative-shell][Functional Core, Imperative Shell]] message, but how does this apply in a world where, supposedly, *everything* is functional? As we'll see, the Haskell equivalent is something like "functional core, ~IO~ shell" - but we're getting ahead of ourselves. I wrote [[https://github.com/harryaskham/tetriskell][my own toy implementation]] as a way of getting to grips with the language, and thought I'd revisit it, rewriting it piece-by-piece in notebook style.

* Strategy
- We'll represent the play area as a [[https://hackage.haskell.org/package/containers-0.4.0.0/docs/Data-Map.html][~Map~]] (think: a tree-backed dictionary) whose keys are coordinates, and whose values are the contents of a grid cell, where the y-coordinate grows from top to bottom.
- We'll make this 10x24, to allow for a 4-row buffer at the top in which to place new pieces.
- Pieces themselves will begin life in a 4x4 grid, and remain that way until they get fixed to the board.
- This lets us implement both collision detection and bounds checks on falling pieces by stepping forward (either by gravity or user input), looking for overlap, and simply rejecting the new game state.
- Eventually, we'll have three threads running:
  - One to progress the game state (lower the blocks, delete full rows, update score, etc)
  - One to draw the game to the screen
  - One to accept user input and act on it
- We'll also implement a simple bot that simulates a few blocks ahead and optimises for keeping the grid as low as possible.

* Imports and Dependencies
We'll start with the imports we need. Haskell is "batteries included" in so far as there are a tonne of widely used, canonical core libraries - but you need to make them available on your system. For example, we'll be using [[https://hackage.haskell.org/package/containers-0.4.0.0/docs/Data-Map.html][~Map~]] a lot, which is part of the ~containers~ package. This needs to be available on your system - there are myriad ways of doing this, but simplest is just running ~$ cabal install containers~. You might be using Stack, or Nix, or something else. The full list of packages we need here are:

- ~containers~
- TODO

If you're following along, you'll want to install them all:

~cabal install containers TODO~

Alright, so say we've got our ~tetris.hs~ blank slate. This is going to be a single-file program, so we'll put everything into a monolithic ~Main~ module.

#+BEGIN_SRC haskell :exports code
-- Every Haskell source file begins with a module definition like this.
module Main where

-- There are lots of Map-related methods; a qualified import avoids naming
-- clashes, and means we can look things up using `M.lookup` rather than
-- simply `lookup`.
-- Ignore the 'Strict' for now - laziness/strictness is out of scope here.
import Data.Map.Strict qualified as M

-- By also importing the Map type directly, we don't need to constantly
-- specify `M.Map` and can just use `Map` directly in our type signatures.
import Data.Map.Strict (Map)

-- Other things we'll need throughout
import Data.List (intercalate, foldl')
import Data.Function ((&))
#+END_SRC

#+RESULTS:
: ghci>

* Establishing the Grid

Now let's think about how we'll represent the game state, the entities within it, and the actions we can take.

We'll need a 2D grid of cells, each of which can be empty or filled with a block, and that block . Whenever you have state in this "one-of-many" form, where you might reach for an enum, in Haskell you can define a sum type:

#+BEGIN_SRC haskell :exports code
:{
-- This is a sum type; we can now use these colour values directly in our code.
-- Yes, we'll be using British English.
data Colour = Blue | Orange | Yellow | Green | Purple | Red | Cyan

-- Another sum type; we either have a block of a certain colour, or empty space.
data Cell = Block Colour | Empty
:}
#+END_SRC

#+RESULTS:

Now we're ready to set up our grid:

#+BEGIN_SRC haskell :exports code
:{
-- This is a type alias - now any time we want a 2-dimensional coordinate,
-- we can use V2 rather than continually specify that we're representing
-- x and y as a tuple of Ints.

-- You get this and more for free in the `linear` package as `Linear.V2`
--- but I want to avoid as many dependencies as possible.
type V2 = (Int, Int)

-- Rather than use a 2D array-of-arrays, we'll just use
-- a map keyed by our ~V2~ coordinate type, whose values are of our `Cell` type.
-- We use a new datatype here rather than an alias, as this will later allow us to
-- attach new behaviour to the Grid in the form of typeclass instances.
-- This gives us a constructor function `Grid`, which accepts a width, height, and
-- `Map` as its arguments and gives us back a value of type `Grid`. That the
-- constructor has the same name as the type is just convention.
data Grid = Grid Int Int (Map V2 Cell)
:}
#+END_SRC

#+RESULTS:

And our first function, a simple constructor:

#+BEGIN_SRC haskell :exports code
:{
-- Right, our first function - this will construct us an empty grid.
-- It's a fairly common pattern to prefix constructors like this with 'mk'.

-- You can think of a `Map` as a list of key-value pairs where it's efficient
-- to pick out any one pair by its key; it's easy to switch back and forth
-- between these `Map` and list-of-pairs representations, and it's an easy
-- way to construct them.

-- The type signature follows the :: and here simply says we take no arguments,
-- and return an instance of the Grid type. Typically, for top-level functions
-- like this, you include a type signature before the implementation, even
-- though the compiler can often figure it out itself.

-- We use a list comprehension to create the `(V2, Cell)` pairs of the grid, and
-- pass this to M.fromList to get our `Map V2 Cell`, i.e. our `Grid`.
-- Note that Haskell range sugar is inclusive, so [1 .. 3] is [1, 2, 3].

-- The $ operator applies the function on the left of it (in this case `Grid`) to
-- everything on the right; it's a common way of avoiding Lisp-style parenthesis
-- overload.
mkEmptyGrid :: Int -> Int -> Grid
mkEmptyGrid width height =
  Grid width height
    $ M.fromList [((x, y), Empty) | x <- [0 .. width - 1] , y <- [0 .. height - 1]]
:}
#+END_SRC

#+RESULTS:

Let's get some output going. We're going to want to be able to pretty-print a bunch of our entities (our grids, our scoreboard) - when we want to implement the same broad concept across multiple disparate types, we draw for a typeclass (similar to a trait in Rust, or maybe an interface in Go). We'll define a ~Pretty~ typeclass - any type that implements this will be convertable to a nicely formatted ~String~[fn:string] which we can later print to the screen[fn:show].

[fn:string] You'll typically be recommended to eschew ~String~ (which is a linked list of characters) for the more efficient ~Text~ type; we don't need to worry about this for a toy application.


[fn:show] There's already the ~Show~ typeclass that does exactly this, and which can be automatically derived for many types, but I tend to think of it as for debugging and inspection purposes - I prefer a separate typeclass for representations intended to be user-facing.


Here ~a~ is a placeholder for the type that will implement the ~Pretty~ class. We're simply saying that anything prettifiable must define a ~pretty~ function that spits out a nice ~String~ representation. Very hand-wavily, Haskell's type signatures are written this way as all functions can be partially applied and are curried by default; for now, a function with a signature of ~foo :: a -> b -> c -> d~ can be thought of as a three argument function taking an ~a~, a ~b~, a ~c~ and returning a ~d~.

#+BEGIN_SRC haskell :exports code
:{
class Pretty a where
  pretty :: a -> String
:}
#+END_SRC

#+RESULTS:

We can make ~Cell~ an instance of this typeclass simply by associating each cell with a character. We can use Haskell's pattern-matching to have ~pretty~ behave differently depending on whether it's given an ~Empty~ cell or a ~Block~ cell. We can also cheat a little, and make the ~Pretty~ representation of a ~Colour~ be a terminal escape code we can use to give colour to the blocks by using it as a prefix.

#+BEGIN_SRC haskell :exports code
:{
instance Pretty Colour where
  pretty Red = "\x1b[31m"
  pretty Blue = "\x1b[34m"
  pretty Cyan = "\x1b[36m"
  pretty Yellow = "\x1b[33m"
  pretty Green = "\x1b[32m"
  pretty Purple = "\x1b[35m"
  pretty Orange = "\x1b[37m"
:}
#+END_SRC

#+RESULTS:

#+BEGIN_SRC haskell :exports code
:{
instance Pretty Cell where
  pretty Empty = "."
  pretty (Block colour) = pretty colour <> "█"
:}
#+END_SRC

#+RESULTS:

The ~<>~ is shorthand for ~mconcat~ - a member of the ~Monoid~ typeclass, which roughly represents things that can be empty, and can be joined together. ~String~ is a ~Monoid~ so ~<>~ just concatenates them.

We're ready to prettify our ~Grid~. Since we're operating over collections of things, we can start using higher-order functions; in Haskell, ~fmap~ from the ~Functor~ typeclass lets you apply a function to the inhabitants of any instance of a given ~Functor~. A list is an instance of ~Functor~, and so for some list ~xs~, ~fmap f xs~ just operates like the ~map(f, xs)~ function you find over lists in most other languages.

Helper functions and intermediate values defined in ~where~ blocks are available in the above scope. Type signatures are optional, but I've included them for clarity - they can also help the compiler tell you when you've gone off track. I've included some alternative equivalent implementations of ~prettyRow~ here; I won't keep doing this, but it gives you a sense of the different ways one can construct functions.

We use ~M.!~ to look up keys in our grid; this is unsafe, and can throw an error. A nicer way would be to use ~M.lookup~, which returns a ~Maybe Cell~ here, meaning we'd have to handle the ~Nothing~ case (i.e. out of bounds) and the ~Just cell~ case separately. We know we're within bounds here, so we'll keep it simple, but it's worth knowing.

#+BEGIN_SRC haskell :exports code
:{
instance Pretty Grid where
  pretty (Grid width height grid) = intercalate "\n" (prettyRow <$> rows) -- <$> is just an inline fmap
    where
      rows :: [[Cell]]
      rows = [[grid M.! (x, y) | x <- [0 .. width - 1]] | y <- [0 .. height - 1]]
      prettyRow :: [Cell] -> String
      prettyRow row = concatMap pretty row
      -- With eta-reduction:
      -- prettyRow = concatMap pretty
      -- With explicit mapping:
      -- prettyRow row = concat (fmap pretty row)
      -- Using the fmap operator:
      -- prettyRow row = concat (pretty <$> row)
      -- Using the Monad instance of List (don't worry):
      -- prettyRow row = pretty =<< row
:}
#+END_SRC

#+RESULTS:

Here we've converted back from our ~Map~ representation of the ~Grid~ to a ~List~-based one, in order to more easily convert it to a list of ~String~ that we can join (~intercalate~ in Haskell) together with newlines inbetween.

We can finally print our grid! It's nothing special, but here we go:

#+BEGIN_SRC haskell :exports both
:{
putStrLn $ pretty (mkEmptyGrid 10 24)
:}
#+END_SRC

#+RESULTS:
#+begin_example
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
..........
#+end_example

Alright!

* Making Some Tetrominos
Let's make the pieces. We'll represent them as another sum type, and take advantage of Haskell's laziness to construct an infinite stream of pieces, in chunks of seven, where each of the seven chunks is a shuffled collection containing every piece (per the *official rules*). This'll let us easily draw the next piece, as well as enabling a simple lookahead for a next-piece preview.

We'll encode the actual shapes by the coordinates of their full blocks, letting us specify their colour as well. We'll use some helpers to let us quickly set coloured blocks on an empty grid. Eventually we'll have a function that transforms a ~Grid~ into a copy of itself containing one new coloured block - we can then ~fold~ this function, using an empty 4x4 grid as the initial state, over the coordinates of the piece, which will add the blocks one by one, giving us the finished piece.

#+BEGIN_SRC haskell :exports code
:{
-- By deriving `Enum` and `Bounded` typeclasses here, we'll be able to easily
-- get a list of all inhabitants of the `Piece` type later on.
-- We derive `Show` (which lets us print e.g. PieceSquare) for display purposes later.
data Piece
   = PieceL
   | PieceR
   | PieceSquare
   | PieceS
   | PieceZ
   | PieceT
   | PieceLine
   deriving (Show, Enum, Bounded)

-- No holy wars over these, please.
pieceColour :: Piece -> Colour
pieceColour PieceL = Orange
pieceColour PieceR = Blue
pieceColour PieceSquare = Yellow
pieceColour PieceS = Green
pieceColour PieceZ = Red
pieceColour PieceT = Purple
pieceColour PieceLine = Cyan

-- We'll keep all our pieces anchored to the bottom of the 4x4 grid
-- Remember, the y coordinate increases from top to bottom.
pieceCoordinates :: Piece -> [V2]
pieceCoordinates PieceL = [(1, 3), (1, 2), (1, 1), (2, 3)]
pieceCoordinates PieceR = [(1, 3), (1, 2), (1, 1), (2, 1)]
pieceCoordinates PieceSquare = [(1, 3), (1, 2), (2, 3), (2, 2)]
pieceCoordinates PieceS = [(0, 3), (1, 3), (1, 2), (2, 2)]
pieceCoordinates PieceZ = [(0, 2), (1, 2), (1, 3), (2, 3)]
pieceCoordinates PieceT = [(0, 3), (1, 3), (2, 3), (1, 2)]
pieceCoordinates PieceLine = [(1, 3), (1, 2), (1, 1), (1, 0)]

-- By only passing the first argument here, we get back a partially applied
-- function; this is a new function of type `Grid -> V2 -> Grid` which is
-- exactly what we need for our fold. It's a bit of an awkward argument
-- ordering for anything other than a fold.
withBlock :: Colour -> Grid -> V2 -> Grid
withBlock colour (Grid width height grid) xy =
  Grid width height (M.insert xy (Block colour) grid)

-- Adds a whole piece to the grid one block at a time
withPiece :: Piece -> Grid -> Grid
withPiece piece grid =
   foldl'
     (withBlock (pieceColour piece))
     grid
     (pieceCoordinates piece)

-- Here the (&) operator is just the reverse of ($) - everything to the
-- right is applied to the left. Useful for builder functions like these.
mkPieceGrid :: Piece -> Grid
mkPieceGrid piece = mkEmptyGrid 4 4 & (withPiece piece)
:}
#+END_SRC

#+RESULTS:

Whew, okay. Let's give ourselves a nice way of inspecting these pieces - we'll use this for things like next piece preview. We can just pretty-print the containing grid; here we use point-free style to omit the argument. The ~(.)~ operator composes functions right-to-left, so since we want to first convert to a grid, and then pretty-print, we can write:

#+BEGIN_SRC haskell :exports code
:{
instance Pretty Piece where
  pretty = pretty . mkPieceGrid
:}
#+END_SRC

#+RESULTS:

Notice how we take our grid as an argument, and return ostensibly a new one; in some languages this would be expensive, but Haskell's functional data structures make this a cheap operation, and let us pass around and create updated versions of state without needing to worry about mutation. We can just think in terms of pure transformations of our entities.[fn:foldl]

[fn:foldl] The use of ~foldl'~ here does two things: we fold from the left (irrelevant in this case, but important sometimes), and we fold strictly - that is, we don't accumulate a load of unevaluated thunks and overflow the stack. Again, never going to happen in our toy example, but worth knowing.


Let's see if we got that right by pretty-printing these pieces:

#+BEGIN_SRC haskell :exports both
:{
-- We can use the `Enum` and `Bounded` instances here to range over all pieces.
-- To make this work, we need to add the type hint `:: Piece`.
-- `mapM_` just lets us run a function with side effects and with no return value
-- like `putStrLn` over a collection of things.
putStrLn $ intercalate "\n\n" [show piece <> "\n" <> pretty piece
                               | piece <- [minBound .. maxBound :: Piece]]
:}
#+END_SRC

#+RESULTS:
#+begin_example
PieceL
....
.█..
.█..
.██.

PieceR
....
.██.
.█..
.█..

PieceSquare
....
....
.██.
.██.

PieceS
....
....
.██.
██..

PieceZ
....
....
██..
.██.

PieceT
....
....
.█..
███.

PieceLine
.█..
.█..
.█..
.█..
#+end_example

Looks good to me. While we're here, let's implement piece rotation.

TODO

* Representing the Game State

Now we'll create the type we'll be using to store all state about the ongoing game. This is a product type; rather than "one-of-many", this represents a collection of many simultaneous values. You can think of it like a struct.

#+BEGIN_SRC haskell :exports code
:{
data Game = Game {
  grid :: Grid,
  score :: Int
}
-- TODO more
:}
#+END_SRC

#+RESULTS:

TODO
